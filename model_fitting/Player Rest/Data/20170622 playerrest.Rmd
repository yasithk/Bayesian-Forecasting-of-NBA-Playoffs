---
title: "R Notebook"
output: html_notebook
---


```{r}
library(rstan)
library(bayesplot)
```

```{r}
setwd("~/Honours/Model/Model Fitting/Player Rest/Data")
#library(readr)
nba <- read.csv(file = "daysofrest.csv", header = TRUE)
# 
# nba$score_diff <- nba$H.Score - nba$A.Score
# 
# nba$nteams <- length(unique(nba$Home.Team))
# nba$ngames <- length(nba$score_diff)
# nba$nweeks <- floor(2*nba$ngames/nba$nteams) 
```

#STAN code
this is the stan code for incorporating the days of rest the home and away team has had prior to their game. 

I have used a classification for 1,2,3, 4 of more days of rest and estimated coefficients for each. 

**I have not combined the unique home effect with the team rest here. 
```{stan output.var="nba_model"}
data{
int<lower=1> nteams; 
int<lower=1> ngames;
int<lower=1> nweeks;
int<lower=0, upper=1> resthome2[ngames]; //2 days of rest of home team
int<lower=0, upper=1> resthome3[ngames]; //3 days of rest for home team
int<lower=0, upper=1> resthome4[ngames]; // 4 or more days of rest for home team
int<lower=0, upper=1> restaway2[ngames]; // 2 days rest away team
int<lower=0, upper=1> restaway3[ngames]; // 3 day of rest for away team
int<lower=0, upper=1> restaway4[ngames]; // 4 or more days of rest for away team

int<lower=1> home_week[ngames]; // week number for the home team
int<lower=1> away_week[ngames]; // week number for the away team
int<lower=1, upper=nteams> home_team[ngames]; // home team ID (1, ..., 20)
int<lower=1, upper=nteams> away_team[ngames]; // away team ID (1, ..., 20)
vector[ngames] score_diff; // home_goals - away_goals

}
parameters {
real b_home; // the effect of hosting the game in mean of score_diff dist.
//real b_prev; // regression coefficient of prev_perf


//real beta1; //regression coefficient of days of rest home
real beta2h; //regression coefficient of days of rest aways
real beta3h;
real beta4h;
real beta2a;
real beta3a;
real beta4a;

real<lower=0> sigma_a0; // teams ability variation
real<lower=0> tau_a; // hyper-param for game-to-game variation
real<lower=1> nu; // t-dist degree of freedom
real<lower=0> sigma_y; // score_diff variation
row_vector<lower=0>[nteams] sigma_a_raw; // game-to-game variation
matrix[nweeks,nteams] eta_a; // random component
}

transformed parameters {
matrix[nweeks, nteams] a; // team abilities
row_vector<lower=0>[nteams] sigma_a; // game-to-game variation
vector[ngames] a_diff;
vector[ngames] mu;
a[1] =  sigma_a0 * eta_a[1]; // initial abilities (at week 1)
sigma_a = tau_a * sigma_a_raw; //performance evolves from week to week
  for (w in 2:nweeks) {
    a[w] = a[w-1] + sigma_a .* eta_a[w]; // evolution of abilities
  }
  for (g in 1:ngames) {
    a_diff[g] = a[home_week[g],home_team[g]] - a[away_week[g],away_team[g]];
    // additional reg parameters e.g. rest of home, rest of away team
    mu[g] = a_diff[g] + b_home  + beta2h*resthome2[g] + beta3h*resthome3[g] + beta4h*resthome4[g] + beta2a*restaway2[g]
    + beta3a*restaway3[g] + beta4a*restaway4[g];
    //(come in via the data section)
  }
}

model {

// Priors
nu ~ gamma(2,0.1);
//b_prev ~ normal(0,1);

beta2h ~ normal(0,10); //prior on beta1
beta3h ~ normal(0,10); //prior on beta2
beta4h ~ normal(0,10);
beta2a ~ normal(0,10);
beta3a ~ normal(0,10);
beta4a ~ normal(0,10);

sigma_a0 ~ normal(0,1);
sigma_y ~ cauchy(0,5); 
b_home ~ normal(0,100); //blangiardo uses N(0,0.0001)
sigma_a_raw ~ normal(0,1);
tau_a ~ cauchy(0,1);
to_vector(eta_a) ~ normal(0,1);
// Likelihood
score_diff ~ student_t(nu, mu, sigma_y);
}

generated quantities {
vector[ngames] score_diff_rep;
for (g in 1:ngames)
score_diff_rep[g] = student_t_rng(nu, a[home_week[g], home_team[g]] -a[away_week[g],away_team[g]]+b_home, sigma_y);
}

```

##Fitting the model

```{r}
nba <- read.csv("daysofrest.csv", header = TRUE)
#THE DATASET ATTACHED HAS TREATED NaN AS 0S
rstan_options(auto_write = TRUE)
options(mc.cores = parallel::detectCores())

nsamples <- 2000
dat <- NULL
dat$nteams <- 30
dat$ngames <- 1230
dat$nweeks <- 82
dat$home_team <- nba$H.Team.ID
dat$away_team <- nba$Away.Team.ID
dat$home_week <- nba$home_week
dat$away_week <- nba$away_week
dat$score_diff <- nba$H.Score - nba$A.Score

dat$resthome2 <- as.numeric(nba$DaysofRest.Home.Team == 2)
dat$resthome3 <- as.numeric(nba$DaysofRest.Home.Team == 3)
dat$resthome4 <- as.numeric(nba$DaysofRest.Home.Team == 0 | nba$DaysofRest.Home.Team > 3)
dat$restaway2 <- as.numeric(nba$DaysofRest.Away.Team == 2)
dat$restaway3 <- as.numeric(nba$DaysofRest.Away.Team == 3)
dat$restaway4 <- as.numeric(nba$DaysofRest.Away.Team == 0 | nba$DaysofRest.Home.Team > 3)

fit <- sampling(nba_model, chains = 4, iter = (nsamples/2), data = dat)

vectofOBS <- dat$score_diff  
```

```{r}
library(shinystan)
launch_shinystan(fit)

```

